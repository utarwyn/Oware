allprojects {
    apply plugin: 'application'
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    compileJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.12'
        compileOnly('org.projectlombok:lombok:1.16.20')
        annotationProcessor 'org.projectlombok:lombok:1.16.20'
        compile 'com.google.code.gson:gson:2.8.5'
    }

    tasks.withType(Jar) {
        destinationDir = file("$rootDir/out/artifacts/$rootProject.name/")
    }

    sourceSets {
        main {
            resources {
                srcDirs = ["src/main/java", "src/main/resources"]

                // Required to add fxml and xml to the jar
                includes = ["**/*.fxml", "**/*.xml", "**/*.css", "**/*.png", "**/*.jpg"]
            }
        }
    }
}

project('client') {
    mainClassName = "fr.ensicaen.oware.client.applications.Connection"
    jar {
        // Tune the java manifest, to tell about the main class
        manifest {
            attributes(
                    'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                    'Main-Class': mainClassName
            )
        }
        // tells to put all dependencies jars to our fat jar
        from {
            configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
        }
    }
}

project('server') {
    mainClassName = "fr.ensicaen.oware.server.Main"
    jar {
        // Tune the java manifest, to tell about the main class
        manifest {
            attributes(
                    'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                    'Main-Class': mainClassName
            )
        }
        // tells to put all dependencies jars to our fat jar
        from {
            configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
        }
    }
}